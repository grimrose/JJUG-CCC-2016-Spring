buildscript {
  repositories {
    mavenCentral()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE'
    classpath 'org.ajoberstar:gradle-git:1.4.2'
    classpath 'gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.16'
    classpath "com.h2database:h2:1.4.191"
    classpath "gradle.plugin.com.boxfuse.client:flyway-release:4.0.1"
  }
}

apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'application'

apply plugin: "org.flywaydb.flyway"
apply plugin: "com.gorylenko.gradle-git-properties"

def defaultEncoding = "UTF-8"

tasks.withType(AbstractCompile) { options.encoding = defaultEncoding }
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

mainClassName = 'sample.Main'

jar {
  manifest {
    attributes 'Main-Class': project.mainClassName
  }
}

repositories {
  jcenter()
}

dependencies {
  compile 'org.codehaus.groovy:groovy-all:2.4.6'
  compile 'org.springframework.boot:spring-boot-starter-web:1.3.3.RELEASE'
  compile 'org.springframework.boot:spring-boot-starter-actuator:1.3.3.RELEASE'

  testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
  testCompile 'junit:junit:4.12'

  testCompile( 'com.athaydes:spock-reports:1.2.11' ) { transitive = false }

  testCompile 'org.slf4j:slf4j-api:1.7.13'
  testCompile 'org.slf4j:slf4j-simple:1.7.13'
}

// please use ./git path
gitProperties {
  gitRepositoryRoot = new File("${project.rootDir}/../")
}

import org.ajoberstar.grgit.*
task printHeadHash << {
  def grgit = Grgit.open(project.file("${project.rootDir}/../"))
  println grgit.head().abbreviatedId
}

flyway {
    url = "jdbc:h2:file:${projectDir}/build/sample"
    user = 'sa'
}
